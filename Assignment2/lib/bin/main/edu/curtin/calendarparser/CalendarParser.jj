options {
    BUILD_PARSER = true;
    BUILD_TOKEN_MANAGER = true;
}

PARSER_BEGIN(CalendarParser)
package edu.curtin.calendarparser;
import java.io.*;
import edu.curtin.event.Event;
import edu.curtin.event.EventStorage;
import edu.curtin.plugin.PluginClass;
import edu.curtin.plugin.PluginStorage;
import edu.curtin.script.Script;
import edu.curtin.script.ScriptStorage;
import java.time.LocalDate;
import java.time.LocalTime;

public class CalendarParser 
{
    public static void parse(String fileName) throws ParseException, IOException
    {
        CalendarParser parser = new CalendarParser(new FileInputStream(fileName));
        parser.start();
    }
}
PARSER_END(CalendarParser)

SKIP:{" " | "\n" | "\r" | "\t" }

TOKEN : 
{
    < EVENT_DECLARATION: "event" > |
    < PLUGIN_DECLARATION: "plugin" > |
    < SCRIPT_DECLARATION: "script" > |

    < DATE: (["0"-"9"])+ "-" (["0"-"9"])+ "-" (["0"-"9"])+ > |
    < TIME: (["0"-"9"])+ ":" (["0"-"9"])+ ":" (["0"-"9"])+ > |
    < DURATION: (["0"-"9"])+ > |
    < ALL_DAY: "all-day" >  |

    < STRING_QUOTE: "\"" (~["\""])* "\"" > |
    < SCRIPT_CONTENT: "\"" (~["\""] | "\"\"")* "\"" > |
    < STRING_NORMAL: (~[" ", "\n", "\r", "{", "}", ":", ","])+ >
}

void start() : {}
{
    (event() | plugin() | script())*
}

void event() : 
{
    Token date, startTime, duration, title, tokAllDay;
    LocalDate eventDate;
    LocalTime eventTime = null;
    int eventDuration = 0;
    String eventTitle;
    boolean allDay = false;
    EventStorage storage = EventStorage.getInstance();
}
{
    (
        < EVENT_DECLARATION > date = <DATE>
        {
            eventDate = LocalDate.parse(date.image);
        }
        (
            startTime = <TIME>
            {
                eventTime = LocalTime.parse(startTime.image);
            }
            duration = <DURATION>
            {
                eventDuration = Integer.parseInt(duration.image);
            }
            |
            tokAllDay = <ALL_DAY>
            {
                allDay = true;
            }
        )
        title = <STRING_QUOTE>
        {
            eventTitle = title.image;
        }
        {
            if(allDay)
            {
                Event newEvent = new Event(eventDate, allDay, eventTitle);
                storage.addEvent(newEvent);
            }
            else
            {
                Event newEvent = new Event(eventDate, eventTime, eventDuration, eventTitle);
                storage.addEvent(newEvent);
            }
        }
    )
}

void plugin() : 
{
    Token plugin_id, arguments, key, value;
    PluginStorage storage = PluginStorage.getInstance();
    String plugID;
}
{
    (
        < PLUGIN_DECLARATION > plugin_id = < STRING_NORMAL >
        {
            plugID = plugin_id.image;
        }
        (
            "{" getKeys(plugID) ("," getKeys(plugID))* "}"
        )
    )
}

void getKeys(String plugID):
{
    Token key, value;
    String plugKey;
    String plugValue;
    PluginStorage storage = PluginStorage.getInstance();
}
{
    key = <STRING_NORMAL> ":" value = <STRING_QUOTE>
    {
        plugKey = key.image;
        plugValue = value.image;
        PluginClass newPlug = new PluginClass(plugID, plugKey, plugValue);
        storage.addPlugin(newPlug);
    }
}

void script() : 
{
    Token script;
    String scriptContent;
    ScriptStorage storage = ScriptStorage.getInstance();
}
{
    < SCRIPT_DECLARATION > script = < SCRIPT_CONTENT >
    {
        scriptContent = script.image;
        Script newScript = new Script(scriptContent);
        storage.addScript(newScript);
    }
}